dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.54)

dnl Name your plug-in here
m4_define([plugin_name], [refocus-it])

dnl These three define the plug-in version number
m4_define([plugin_major_version], [2])
m4_define([plugin_minor_version], [0])
m4_define([plugin_micro_version], [0])

m4_define([plugin_version],
          [plugin_major_version.plugin_minor_version.plugin_micro_version])

AC_INIT([plugin_name], [plugin_version], "Lukas.Kunc@seznam.cz")

AC_DEFINE(PLUGIN_NAME, PACKAGE_NAME, [Plug-In name])
AC_DEFINE(PLUGIN_VERSION, PACKAGE_VERSION, [Plug-In version])

AC_DEFINE(PLUGIN_MAJOR_VERSION, plugin_major_version, [Plug-In major version])
AC_DEFINE(PLUGIN_MINOR_VERSION, plugin_minor_version, [Plug-In minor version])
AC_DEFINE(PLUGIN_MICRO_VERSION, plugin_micro_version, [Plug-In micro version])

AC_CONFIG_SRCDIR([src/image.c])
AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(no-define)

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_MAKE_SET
AC_HEADER_STDC

ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"

AM_MAINTAINER_MODE

dnl Use -Wall if we have gcc.
changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl

dnl COMMAND-LINE
AC_ARG_WITH([cmdline], AC_HELP_STRING([--with-cmdline],[Install cmd-line version of refocus-it [default=yes]]), with_cmdline=$withval, with_cmdline=yes)
AM_CONDITIONAL(REFOCUS_CMDLINE, test x$with_cmdline = xyes)

dnl GIMP PLUG-IN
AC_ARG_WITH([gimp], AC_HELP_STRING([--with-gimp],[Install GIMP plug-in version of refocus-it [default=yes]]), with_gimp=$withval, with_gimp=yes)
AM_CONDITIONAL(REFOCUS_GIMP, test x$with_gimp = xyes)

dnl INLINES
AC_ARG_ENABLE([inlines], AC_HELP_STRING([--enable-inlines],[Use inlines instead of macros [default=no]]), enable_inlines=$enableval, enable_inlines=no)

dnl DEBUG
AC_ARG_ENABLE([debug], AC_HELP_STRING([--enable-debug],[Enable debugging [default=no]]), , enable_debug=no)

dnl DOUBLES
AC_ARG_ENABLE([doubles], AC_HELP_STRING([--enable-doubles],[Use doubles instead of floats [default=no]]), enable_doubles=$enableval, enable_doubles=no)

if test x$enable_debug = xno ; then
	AC_DEFINE(NDEBUG,, Enable debugging)
fi
										if test x$enable_inlines = xyes ; then
	AC_DEFINE(USE_INLINES,, Use inlines instead of macros)
	AC_C_INLINE
fi

if test x$enable_doubles = xyes ; then
	AC_DEFINE(USE_DOUBLES,, Use doubles instead of floats)
fi

dnl i18n stuff
LOCALEDIR='${datadir}/locale'
AC_SUBST(LOCALEDIR)

GETTEXT_PACKAGE=refocus-it
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE",
			    [The gettext translation domain.])

dnl Add the languages which your application supports here.
ALL_LINGUAS="cs"

AC_PROG_INTLTOOL
AM_GLIB_GNU_GETTEXT

AC_CHECK_LIB([m], [cos])
AC_CHECK_HEADERS([libintl.h locale.h stdlib.h string.h strings.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

# Checks for library functions.
AC_FUNC_MALLOC
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(sqrt)
AC_CHECK_FUNCS(bind_textdomain_codeset)

dnl Command-line version
if test x$with_cmdline = xyes ; then
	dnl Checks for header files.
	AC_CHECK_HEADERS([unistd.h])
	dnl Checks for library functions.
	AC_CHECK_FUNCS([setlocale memset strerror strrchr])
	AC_CONFIG_FILES(cmd-line/Makefile)
fi


dnl Gimp-plugin
if test x$with_gimp = xyes ; then
	PKG_CHECK_MODULES(GIMP, gimp-2.0 gimpui-2.0)

	AC_SUBST(GIMP_CFLAGS)
	AC_SUBST(GIMP_LIBS)

	GIMP_LIBDIR=`$PKG_CONFIG --variable=gimplibdir gimp-2.0`
	AC_SUBST(GIMP_LIBDIR)

	DATADIR='${datadir}/${PLUGIN_NAME}'

	AC_SUBST(DATADIR)

	AC_MSG_CHECKING([if GTK+ is version 2.3.0 or newer])
	if $PKG_CONFIG --atleast-version=2.3.0 gtk+-2.0; then
		have_gtk_2_3=yes
	else
		have_gtk_2_3=no
	fi
	AC_MSG_RESULT($have_gtk_2_3)

	if test "x$have_gtk_2_3" != "xyes"; then
		CPPFLAGS="$CPPFLAGS -DG_DISABLE_DEPRECATED -DGDK_DISABLE_DEPRECATED -DGTK_DISABLE_DEPRECATED"
	fi
	AC_CONFIG_FILES([help/Makefile help/images/Makefile help/C/Makefile gimp-plugin/Makefile])
fi


AC_CONFIG_FILES([Makefile src/Makefile po/Makefile.in doc/Makefile])

AC_OUTPUT
